#! /software/bin/perl

use strict;
use warnings;
use English qw{-no_match_vars};
use Carp;
use Readonly;

Readonly::Scalar our $ILLUMINA_TAG_SET => {
    ATCACG => 1,
    CGATGT => 2,
    TTAGGC => 3,
    TGACCA => 4,
    ACAGTG => 5,
    GCCAAT => 6,
    CAGATC => 7,
    ACTTGA => 8,
    GATCAG => 9,
    TAGCTT => 10,
    GGCTAC => 11,
    CTTGTA => 12,
};

my $file = shift @ARGV;
my $only_top_x = shift @ARGV;
my $max_number_read = shift @ARGV;
my $max_number_reads = 10_000;
my $i = 0;

open ( my $fh, q{<}, $file ) or croak qq{Unable to open $file for reading};

my %h;

while ( (!$max_number_read || $i < $max_number_reads) && (my $line = readline $fh) ) {
  if ( ( $i++%4 ) == 1 ) {
    $h{substr($line,0,8)}++;
  }
}

close $fh or croak qq{Unable to close $file handle};

my @s;
while ( my($k,$v) = each %h ) {
  push @s, [$k,$v];
}

$i = 1;
print qq{Tag\t\tCount\tMatchTag\n} or carp qq{Tag\t\tCount\tMatchTag\n};
foreach my $tag ( sort { $b->[1] <=> $a->[1] } @s ) {
  if ( $only_top_x and ($i > $only_top_x) ) { last; }
  my $result = join"\t", @{$tag}, ( $ILLUMINA_TAG_SET->{substr($tag->[0],0,6)} || q{} );
  print qq{$result\n} or carp $result;
  $i++;
}

__END__

=head1 NAME

tag_identifier

=head1 VERSION

1

=head1 SYNOPSIS

  tag_identifier xxxx_x_t.fastq
  tag_identifier xxxx_x_t.fastq 20
  tag_identifier xxxx_x_t.fastq 20 1

=head1 DESCRIPTION

A quick script which passes the fastq file generated for the tag read to find all the tags found.
It then organises by count (highest first) and also compares the first 6 bases to see if it matches
one of the illumina12 tagset.

The first argument it the tag file fastq filename
Second argument is the top x tags found to return (since the list can be very long) - note a zero here returns all
Third argument is a boolean which tells it only to go through the first 10_000 reads found, which
is a significant speed up

This came initially from a one-liner written by David Jackson

=head1 USAGE

  As SYNOPSIS

=head1 REQUIRED ARGUMENTS

  Filename

=head1 OPTIONS

  top x tags to return
  only first 10_000

=head1 EXIT STATUS

0

=head1 SUBROUTINES/METHODS

=head1 DIAGNOSTICS

=head1 CONFIGURATION

=head1 DEPENDENCIES

=over

=item strict

=item warnings

=item Carp

=item English -no_match_vars

=item Readonly

=back

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

Most code has bugs and/or limitations, and this code is likely no exception. Email if you have any problems, concerns, patches or updates.

=head1 AUTHOR

Andy Brown

=head1 LICENSE AND COPYRIGHT

Copyright (C) 2010 Andy Brown (setitesuk@gmail.com)

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.

